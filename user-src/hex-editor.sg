// (Built with modified version of Sage compiler -- "associated-consts-my-os" branch -- to target our custom OS)

enum Option<T> {
    Some(T),
    Nothing,
}

enum Command {
    Write {
        start: Int,
        end: Option<Int>,
        value: Int,
    },
    Examine {
        start: Int,
        end: Option<Int>
    },
    Clear(Option<Int>),
    Grow(Int),
    Shrink(Int),
    Quit,
    Report,
    Help,
    Unknown,
}

struct Memory {
    data: &mut Int,
    length: Int,
    capacity: Int,
}

const HELP_MESSAGE = "HELP
====
Commands:
1. Examine a byte in memory:
    '<address>'           to examine a single byte
    '<address>.<address>' to examine a range of memory

2. Write to a byte in memory:
    '<address>=<value>'           to write a single byte
    '<address>.<address>=<value>' to write a range of memory

3. Clear memory:
    '!'         to clear all memory to zero
    '! <value>' to clear all memory to a specific value

4. Grow or shrink memory:
    '+ <value>' to grow memory to a specific size
    '- <value>' to shrink memory to a specific size

5. Report:
    'r' to print the current memory state
    'h' to print this help message

6. Quit:
    'q' to quit
    
All numbers are hexadecimal.";

extern memset(dst: &mut Cell, value: Cell, count: Int);

def main() {
    let mut m = Memory_default();
    print("+ Adam's Hex Editor\n");
    print("  Welcome to Adam's hex editor!\n");
    print("+ About\n");
    print("  This is written in the Sage programming language: https:\/\/adam-mcdaniel.net\/sage\n");
    print("  And compiled as an ELF userspace application for our custom OS!\n");
    print("+ Usage\n");
    print("  This hex editor supports the following commands:\n");
    print("    Examine a byte in memory:\n");
    print("      '<address>'           to examine a single byte\n");
    print("      '<address>.<address>' to examine a range of memory\n");
    print("    Write to a byte in memory:\n");
    print("      '<address>=<value>'           to write a single byte\n");
    print("      '<address>.<address>=<value>' to write a range of memory\n");
    print("    Clear memory:\n");
    print("      '!'         to clear all memory to zero\n");
    print("      '! <value>' to clear all memory to a specific value\n");
    print("    Grow or shrink memory:\n");
    print("      '+ <value>' to grow memory to a specific size\n");
    print("      '- <value>' to shrink memory to a specific size\n");
    print("    Report:\n");
    print("      'r' to print the current memory state\n");
    print("      'h' to print this help message\n");
    print("    Quit:\n");
    print("      'q' to quit\n");
    print("  All numbers are hexadecimal.\n");


    // print("+ Adam's Calculator\n");
    // print("  Welcome to Adam's calculator!\n");
    // print("+ About\n");
    // print("  This is written in the Sage programming language: https:\/\/adam-mcdaniel.net\/sage\n");
    // print("  And compiled as an ELF userspace application for our custom OS!\n");
    // print("+ Usage\n");
    // print("  This calculator supports the following infix operators: \"+\", \"-\", \"*\", \"/\", \"%\"\n");
    // print("  Type a math expression (integers only) to evaluate it.\n");
    // print("  Type \"exit\" to quit the program.\n");
    // print("  Example expression syntax: (4 + 6) * ((8 - 2) / 2) + (9 % 5)\n");

    let mut is_done = False;
    while !is_done {
        print("> ");
        let result = Command_read();
        match result {
            of Examine {
                start,
                end=of Some(end)
            } => {
                if (end < start) {
                    print("Invalid range\n");
                    return ();
                }

                if (start > m.length) {
                    print("Invalid start address\n");
                    return ();
                }

                if (end > m.length) {
                    print("Invalid end address\n");
                    return ();
                }

                for let mut i=start; i<end; i+=1 {
                    print_byte(Memory_get(&mut m, i));
                    print(" ");
                }
                print("\n");
            },
    
            of Examine {
                start,
                end=of Nothing
            } => {
                if (start > m.length) {
                    print("Invalid start address\n");
                    return ();
                }

                print_byte(Memory_get(&mut m, start));
                print("\n");
            },
    
            of Write {
                start,
                end=of Some(end),
                value
            } => {
                if (end < start) {
                    print("Invalid range\n");
                    return ();
                }

                if (start > m.length) {
                    print("Invalid start address\n");
                    return ();
                }

                if (end > m.length) {
                    print("Invalid end address\n");
                    return ();
                }

                // for let mut i=start; i<end; i+=1 {
                //     Memory_set(&mut m, i, value);
                // }
                if (end >= m.length) {
                    Memory_extend(&mut m, end - m.length + 1);
                }
                memset(&mut (m.data[start]) as &mut Cell, value, end - start);
            },
            
            of Write {
                start,
                end=of Nothing,
                value
            } => {
                if (start > m.length) {
                    print("Invalid start address\n");
                    return ();
                }
                
                if (start >= m.length) {
                    Memory_extend(&mut m, start - m.length + 1);
                }
                // Memory_set(&mut m, start, value);
                memset(&mut (m.data[start]) as &mut Cell, value, 1);
            },
    
            of Clear(of Some(val)) => {
                memset(m.data as &mut Cell, val, m.length);
            },
    
            of Clear(of Nothing) => {
                memset(m.data as &mut Cell, 0, m.length);
            },
    
            of Grow(val) => {
                if val < m.length {
                    print("Cannot grow memory to a smaller size ", val, "\n");
                } else {
                    print("Growing memory to 0x");
                    // print_byte(val);
                    print_hex(val);
                    print(" bytes\n");
                    Memory_extend(&mut m, val - m.length);
                }
            },
    
            of Shrink(val) => {
                if val > m.length {
                    print("Cannot shrink memory to a greater size ", val, "\n");
                } else {
                    print("Shrinking memory to 0x");
                    // print_byte(val);
                    print_hex(val);
                    print(" bytes\n");
                    Memory_shrink(&mut m, m.length - val);
                }
            },
    
            of Quit => {
                print("Goodbye!\n");
                is_done = True;
            },
    
            of Help => {
                print(HELP_MESSAGE, "\n");
            },

            of Report => {
                Memory_print(&m);
            },
    
            _ => {
                print(HELP_MESSAGE, "\n");
            }
        }

    }
    Memory_free(&m);
}

def is_char_between(ch: Char, start: Char, end: Char): Bool {
    return ch as Int >= start as Int && ch as Int <= end as Int;
}

def digit_to_int(ch: Char): Int {
    if (is_char_between(ch, '0', '9')) {
        return ch as Int - '0' as Int;
    } elif (is_char_between(ch, 'A', 'F')) {
        return ch as Int - 'A' as Int + 10;
    } elif (is_char_between(ch, 'a', 'f')) {
        return ch as Int - 'a' as Int + 10;
    } else {
        return 0;
    }
}

def is_digit(ch: Char): Bool {
    return is_char_between(ch, '0', '9') || is_char_between(ch, 'A', 'F') || is_char_between(ch, 'a', 'f');
}

def read_int(): Int {
    extern getint(radix: Int): Int;
    return getint(16);

    // let mut ch = ' ';
    // input(&mut ch);
    // while ch == ' ' || ch == '\t' || ch == '\n' || ch == '\r' {
    //     input(&mut ch);
    // }

    // let mut start = digit_to_int(ch);
    // while is_digit(ch) {
    //     input(&mut ch);
    //     if is_digit(ch) {
    //         start = start * 16 + digit_to_int(ch);
    //     }
    // }

    // return start;
}

def Command_read(): Command {
    let mut ch = ' ';
    while ch {
        input(&mut ch);
        while ch == ' ' || ch == '\t' || ch == '\n' || ch == '\r' {
            input(&mut ch);
        }

        if ch as Int == -1 {
            return Command of Quit;
        }

        if is_digit(ch) {
            // Parse a hex number
            let mut start = digit_to_int(ch);
            while is_digit(ch) {
                input(&mut ch);
                if is_digit(ch) {
                    start = start * 16 + digit_to_int(ch);
                }
            }

            // Check for a range
            if ch == '.' {
                // Parse another int
                input(&mut ch);
                let mut end = digit_to_int(ch);
                while is_digit(ch) {
                    input(&mut ch);
                    if is_digit(ch) {
                        end = end * 16 + digit_to_int(ch);
                    }
                }

                if end < start {
                    print("Invalid range\n");
                    return Command of Unknown;
                } else {
                    // Check for an assignment
                    if ch == '\n' {
                        return Command of Examine {
                            start=start,
                            end=Option<Int> of Some(end)
                        };
                    } elif ch == '=' {
                        let value = read_int();
                        return Command of Write {
                            start=start,
                            end=Option<Int> of Some(end),
                            value=value
                        };
                    } else {
                        return Command of Examine {
                            start=start,
                            end=Option<Int> of Some(end)
                        };
                    }
                }
            } elif ch == '=' {
                let value = read_int();
                return Command of Write {
                    start=start,
                    end=Option<Int> of Nothing,
                    value=value
                };
            } else {
                return Command of Examine {
                    start=start,
                    end=Option<Int> of Nothing
                };
            }
            
            return Command of Unknown;
        } else {
            match ch {
                'h' | 'H' => {
                    return Command of Help;
                },
                'r' | 'R' => {
                    return Command of Report;
                },
    
                'q' | 'Q' | '\0' => {
                    return Command of Quit;
                },

                '+' => {
                    let value = read_int();
                    if value <= 0 {
                        print("Invalid value\n");
                        return Command of Unknown;
                    }
                    return Command of Grow(value);
                },

                '-' => {
                    let value = read_int();
                    if value <= 0 {
                        print("Invalid value\n");
                        return Command of Unknown;
                    }
                    return Command of Shrink(value);
                },

                '\\' | '!' => {
                    let value = read_int();
                    return Command of Clear(Option<Int> of Some(value));
                },
    
                _ => {
                    return Command of Unknown;
                }
            }
        }

    }
    return Command of Unknown;
}

def copy<T>(dst: &mut T, src: &T, count: Int) {
    extern memcpy(dst: &mut Cell, src: &Cell, count: Int);
    memcpy(dst as &mut Cell, src as &Cell, count * sizeof<T>());
}

def allocate<T>(count: Int): &mut T {
    // return alloc(sizeof<T>() * count) as &mut T;
    extern malloc(size: Int): &mut Cell;
    return malloc(sizeof<T>() * count) as &mut T;
}

def reallocate<T>(src: &T, old_size: Int, new_size: Int): &T {
    let dst = allocate<T>(new_size);
    copy<T>(dst, src, old_size);
    // del src;
    extern free(ptr: &Cell);
    free(src as &Cell);
    return dst;
}

def Memory_push(m: &mut Memory, value: Int) {
    while (m.length >= m.capacity) {
        m.capacity *= 2;
    }
    m.data = allocate<Int>(m.capacity);
    m.data[m.length] = value;
    m.length += 1;
}

def Memory_extend(m: &mut Memory, count: Int) {
    while (m.length + count >= m.capacity) {
        m.capacity *= 2;
    }
    m.data = reallocate<Int>(m.data, m.length, m.capacity);
    m.length += count;
}

def Memory_pop(m: &mut Memory): Int {
    if (m.length == 0) {
        return 0;
    }
    m.length -= 1;
    return m.data[m.length];
}

def Memory_shrink(m: &mut Memory, count: Int) {
    if (m.length < count) {
        m.length = 0;
    } else {
        m.length -= count;
    }
}

def Memory_index(m: &mut Memory, index: Int): &mut Int {
    if (index >= m.length) {
        Memory_extend(m, index - m.length + 1);
    }
    return &mut (m.data[index]);
}

def Memory_get(m: &mut Memory, index: Int): Int {
    return *(Memory_index(m, index));
}

def Memory_set(m: &mut Memory, index: Int, value: Int) {
    *(Memory_index(m, index)) = value;
}

def Memory_new(n: Int): Memory {
    return {
        data=allocate<Int>(n),
        length=0,
        capacity=n
    };
}

def Memory_default(): Memory {
    return Memory_new(0x5000);
}

def Memory_free(m: &Memory) {
    // del m.data;
    extern free(ptr: &Cell);
    free(m.data as &Cell);
}

def print_hex(c: Int) {
    if (c < 10) {
        print(('0' as Int + c) as Char);
    } elif (c < 16) {
        print(('A' as Int + c - 10) as Char);
    } else {
        print_hex(c / 16);
        print_hex(c % 16);
    }
}

def print_byte(mut c: Int) {
    c = c & 0xFF;
    let hex = "0123456789ABCDEF";
    print(hex[(c / 16) & 0xF]);
    print(hex[c & 0xF]);
}

def print_addr(mut c: Int) {
    c = c & 0xFFFFFFFF;
    let hex = "0123456789ABCDEF";
    print(hex[(c / 0x10000000) & 0xF]);
    print(hex[(c / 0x1000000) & 0xF]);
    print(hex[(c / 0x100000) & 0xF]);
    print(hex[(c / 0x10000) & 0xF]);
    print(hex[(c / 0x1000) & 0xF]);
    print(hex[(c / 0x100) & 0xF]);
    print(hex[(c / 0x10) & 0xF]);
    print(hex[c & 0xF]);
}

def Memory_print(m: &Memory) {
    print("Memory: Used=0x");
    print_hex(m.length);
    print("/Capacity=0x");
    print_hex(m.capacity);
    print("\n");
    if (m.length == 0) {
        print("Empty\n");
    }
    for let mut i=0; i<m.length; i+=1 {
        // Print address
        if (i % 16 == 0) {
            print_addr(i);
            print(": ");
        }

        print_byte(m.data[i]);
        print(" ");
        if i % 16 == 15 {
            // print ASCII
            print(" | ");
            for let mut j=i-15; j<=i; j+=1 {
                let c = m.data[j];
                if (c >= 32 && c <= 126) {
                    print(c as Char);
                } else {
                    print(".");
                }
            }
            print("\n");
        }
    }

    // print ASCII
    if (m.length % 16 != 0) {
        let i = m.length;
        let j = i - (i % 16);
        for let mut k=i; k<j+16; k+=1 {
            print("   ");
        }
        print(" | ");
        for let mut k=j; k<i; k+=1 {
            let c = m.data[k];
            if (c >= 32 && c <= 126) {
                print(c as Char);
            } else {
                print(".");
            }
        }
        print("\n");
    }
}

main();